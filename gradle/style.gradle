buildscript {
  repositories {
    gradlePluginPortal()
  }
  dependencies {
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.11.0"
  }
}

apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin

spotless {
  enforceCheck false

  format 'misc', {
    target fileTree(projectDir) {
      include '**/.gitignore'
      include '**/*.gradle'
      include '**/*.adoc'
      include '**/*.sh'
      //include '**/*.html'
      include '**/*.js'
      include '**/*.css'
      include '**/*.xsd'
      exclude '**/webapp/**'
      exclude '**/.gradle/**'
      exclude '**/build/**'
      exclude '**/node_modules/**'
    }
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }

  format 'markdown', {
    target fileTree(projectDir) {
      include '**/*.md'
      exclude 'CHANGELOG.md'
      exclude '**/build/**'
      exclude '**/node_modules/**'
    }
    indentWithSpaces(2)
    endWithNewline()
  }

  format 'xml', {
    target fileTree(projectDir) {
      include '**/*.xml'
      exclude '**/build/**'
      exclude '**/node_modules/**'
      exclude '**/bolt-module/src/main/resources/configurators/**'
      exclude '.idea/**'
    }
    eclipseWtp('xml').configFile("${project.rootDir}/gradle/spotless.xml.prefs")
    trimTrailingWhitespace()
  }

  java {
    target fileTree(projectDir) {
      include '**/*.java'
      exclude '**/build/**'
      exclude '**/node_modules/**'
    }
    googleJavaFormat()
  }
}

tasks.register('formatCode') {
  description "Add license and format code."
  group "Axelor"
  dependsOn 'licenseFormat', 'spotlessApply'
}

build.dependsOn 'spotlessApply'
