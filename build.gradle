import com.axelor.gradle.support.ChangelogSupport

plugins {
  id 'com.axelor.app'
  id 'com.adarshr.test-logger'
}

apply plugin: ChangelogSupport
apply from: 'gradle/react.gradle'

repositories {
  mavenCentral()
  maven {
    url = 'https://repository.axelor.com/nexus/repository/maven-public/'
  }
}

try {
  tasks.named('spotlessCheck')
} catch (UnknownTaskException ignored) {
  apply from: './gradle/style.gradle'
}

testlogger {
  theme 'mocha'
}

// Version management
version = moduleVersion
if (!project.hasProperty('finalRelease')) {
  version += '-SNAPSHOT'
}

// Module metadata
group = moduleGroup
description = moduleDescription

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(javaVersion.toInteger())
  }
}

axelor {
  title = moduleTitle
  description = moduleDescription
}

test {
  exclude 'com/axelor/apps/baml/test/**'
}

dependencies {
  if (findProject(':modules:axelor-message') != null) {
    api project(':modules:axelor-message')
  } else {
    implementation "com.axelor.addons:axelor-message:${axelorMessageVersion}"
  }
  if (findProject(':modules:axelor-utils') != null) {
    api project(':modules:axelor-utils')
  } else {
    implementation "com.axelor.addons:axelor-utils:${axelorUtilsVersion}"
  }

  implementation "org.camunda.bpm:camunda-engine:${camundaVersion}"
  implementation "org.camunda.bpm:camunda-engine-plugin-spin:${camundaVersion}"
  implementation "org.camunda.spin:camunda-spin-dataformat-json-jackson:${camundaSpinVersion}"

  implementation "org.apache.groovy:groovy-all:${groovyVersion}"
  implementation "org.eclipse.birt.runtime.3_7_1:Tidy:${birtTidyVersion}"
  implementation "org.apache.commons:commons-exec:${commonsExecVersion}"
  implementation "org.apache.commons:commons-text:${commonsTextVersion}"
  implementation "org.apache.commons:commons-lang3:${commonsLang3Version}"
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
  implementation "org.json:json:${jsonVersion}"
  implementation "org.reflections:reflections:${reflectionsVersion}"

  implementation "ch.qos.logback:logback-core:${logbackVersion}"
  implementation "ch.qos.logback:logback-classic:${logbackVersion}"
  implementation "org.yaml:snakeyaml:${snakeyamlVersion}"
  implementation "jakarta.websocket:jakarta.websocket-api:${websocketApiVersion}"
  implementation "jakarta.websocket:jakarta.websocket-client-api:${websocketApiVersion}"
  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

  testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}


changelog {
  version = moduleVersion
  output.set(file('CHANGELOG.md'))
  inputPath.set(file('changelogs/unreleased'))
  types.set([
    'Feature',
    'Change',
    'Deprecate',
    'Remove',
    'Fix',
    'Security'
  ])
  header.set("${version.get()} (${new Date().format('yyyy-MM-dd')})")
}

//
// Publish
//

apply plugin: 'maven-publish'

ext {
  mavenUsername = project.findProperty('addonsMavenUsername')
  mavenPassword = project.findProperty('addonsMavenPassword')
}

publishing {
  repositories {
    maven {
      def repoPath = project.hasProperty('finalRelease')
          ? 'maven-releases'
          : 'maven-snapshots'
      name = 'maven'
      url = 'https://repository.axelor.com/nexus/repository/' + repoPath
      credentials {
        username = project.mavenUsername
        password = project.mavenPassword
      }
    }
  }
}

/*
* Jacoco
*/

apply plugin: 'jacoco'

jacoco {
  toolVersion = jacocoVersion
}

tasks.withType(JavaCompile) {
  // Only depend on frontCopy if it exists
  if (project.tasks.findByName('frontCopy') != null) {
    mustRunAfter project.tasks.findByName('frontCopy')
  }
}

tasks.named('spotlessJava') {
  mustRunAfter tasks.named('pnpmSetup')
}
tasks.named('spotlessMarkdown') {
  mustRunAfter tasks.named('pnpmSetup')
}

jacocoTestReport {
  reports {
    xml.required
  }
}

test {
  useJUnitPlatform()
  // report is always generated after tests run
  finalizedBy jacocoTestReport
  maxHeapSize = '1G'
}

jacocoTestReport {
  // tests are required to run before generating the report
  dependsOn test
}
