import com.axelor.gradle.tasks.GenerateChangelog

buildscript {
	ext.aopVersion = '6.1.3'
	ext.repos = {
		mavenCentral() {
			content { excludeGroupByRegex "com\\.axelor.*" }
		}
		maven {
			url 'https://plugins.gradle.org/m2/'
			content { excludeGroupByRegex "com\\.axelor.*" }
		}
		maven { url 'https://repository.axelor.com/nexus/repository/maven-public/' }
	}
	repositories repos
	dependencies {
		classpath "com.axelor:axelor-gradle:${aopVersion}"
	}
}

repositories repos

apply from: "version.gradle"
apply plugin: "com.axelor.app"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = "com.axelor.addons"
description "Axelor Studio Module"

axelor {
	title "Axelor Studio"
	description "Axelor Studio Module"
}

test {
	exclude "com/axelor/apps/baml/test/**"
}

dependencies {
	if (findProject(":modules:axelor-message") != null) {
		api project(":modules:axelor-message")
	}
	else {
		api "com.axelor.addons:axelor-message:1.1.0"
	}
	if (findProject(":modules:axelor-utils") != null) {
		api project(":modules:axelor-utils")
	}
	else {
		api "com.axelor.addons:axelor-utils:1.2.1"
	}

	api "org.camunda.bpm:camunda-engine:7.17.0"
	api "org.camunda.bpm:camunda-engine-plugin-spin:7.17.0"
	api "org.camunda.spin:camunda-spin-dataformat-json-jackson:1.17.0"

	api "org.apache.commons:commons-lang3:3.2"
	api "com.opencsv:opencsv:4.6"

	implementation "org.codehaus.groovy:groovy-all:3.0.9"
	implementation "org.eclipse.birt.runtime.3_7_1:Tidy:1"
	implementation "org.apache.commons:commons-exec:1.2"
	implementation "org.apache.commons:commons-text:1.9"
	implementation 'org.jboss.resteasy:resteasy-jaxb-provider:4.7.7.Final'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.4'
	implementation 'org.json:json:20210307'

	compileOnly 'org.projectlombok:lombok:1.18.24'
	annotationProcessor 'org.projectlombok:lombok:1.18.24'

	testCompileOnly 'org.projectlombok:lombok:1.18.24'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

tasks.register('generateChangelog', GenerateChangelog) {
	description "Generate changelog from unreleased entries."
	group "Axelor"
	files = fileTree(projectDir) {
		include 'changelogs/unreleased/*.yml'
		include 'changelogs/unreleased/*.yaml'
	}
}

try {
	tasks.getByPath(":react-components:baml:bamlBuild")
	tasks.getByPath(":react-components:studio:studioBuild")
	tasks.getByPath(":react-components:bpm-webapp:bpmBuild")
	tasks.getByPath(":react-components:bpm-webapp:mapperBuild")
	tasks.getByPath(":react-components:webservices-builder:wsBuilderBuild")

	tasks.getByPath(":react-components:baml:bamlInstall").mustRunAfter(":react-components:bpm-webapp:bpmInstall")
	tasks.getByPath(":react-components:bpm-webapp:bpmInstall").mustRunAfter(":react-components:bpm-webapp:mapperInstall")
	tasks.getByPath(":react-components:bpm-webapp:mapperInstall").mustRunAfter(":react-components:studio:studioInstall")
	tasks.getByPath(":react-components:studio:studioInstall").mustRunAfter(":react-components:webservices-builder:wsBuilderInstall")

	processResources {
		dependsOn ":react-components:baml:bamlBuild"
		dependsOn ":react-components:studio:studioBuild"
		dependsOn ":react-components:bpm-webapp:bpmBuild"
		dependsOn ":react-components:bpm-webapp:mapperBuild"
		dependsOn ":react-components:webservices-builder:wsBuilderBuild"

		from(file("$projectDir/react-components/baml/build")) {
			include "**"
			into "webapp/baml-editor"
		}

		from(file("$projectDir/react-components/studio/build")) {
			include "**"
			into "webapp/studio/custom-model"
		}

		from(file("$projectDir/react-components/bpm-webapp/apps/bpm/build")) {
			include "**"
			into "webapp/wkf-editor"
		}

		from(file("$projectDir/react-components/bpm-webapp/apps/mapper/build")) {
			include "**"
			into "webapp/mapper"
		}

		from(file("$projectDir/react-components/webservices-builder/build")) {
			include "**"
			into "webapp/ws-builder"
		}
	}
} catch (ignored) {
	// ignore
}

//
// Publish
//

apply plugin: 'maven-publish'

ext {
	mavenUsername = project.findProperty("addonsMavenUsername")
	mavenPassword = project.findProperty("addonsMavenPassword")
}

publishing {
	repositories {
		maven {
			def repoPath = project.hasProperty("finalRelease")
					? "maven-releases"
					: "maven-snapshots"
			name = "maven"
			url = "https://repository.axelor.com/nexus/repository/" + repoPath
			credentials {
				username = project.mavenUsername
				password = project.mavenPassword
			}
		}
	}
}
